plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.mikepenz.aboutlibraries.plugin'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.plugin.compose' version "$kotlin_version"
    id 'org.jlleitschuh.gradle.ktlint' version '12.1.2'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdk 35
    if (keystoreProperties.containsKey('storeFile')) {
        signingConfigs {
            upload {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['uploadKeyAlias']
                keyPassword keystoreProperties['uploadKeyPassword']
            }
            release {
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['releaseKeyAlias']
                keyPassword keystoreProperties['releaseKeyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "de.lukasneugebauer.nextcloudcookbook"
        minSdk 24
        targetSdk 35
        versionCode 56
        versionName "0.25.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            debuggable true
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions = ["version"]
    productFlavors {
        full {
            dimension "version"
            signingConfig keystoreProperties.containsKey('storeFile') ? signingConfigs.release : signingConfigs.debug
        }
        googlePlay {
            dimension "version"
            signingConfig keystoreProperties.containsKey('storeFile') ? signingConfigs.upload : signingConfigs.debug
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    namespace 'de.lukasneugebauer.nextcloudcookbook'
    dependenciesInfo {
        includeInApk = false
    }
}

kotlin {
    sourceSets {
        debug {
            kotlin.srcDir("build/generated/ksp/debug/kotlin")
        }
        release {
            kotlin.srcDir("build/generated/ksp/release/kotlin")
        }
    }
}

dependencies {

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.1.5'

    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.9.1'
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
    implementation 'androidx.activity:activity-compose:1.10.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.9.1'
    implementation 'androidx.datastore:datastore-preferences:1.1.7'
    implementation 'androidx.core:core-splashscreen:1.0.1'

    implementation platform('androidx.compose:compose-bom:2025.06.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'

    implementation "androidx.compose.material:material"
    implementation "androidx.compose.material3:material3"
    implementation 'androidx.compose.material:material-icons-extended'

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    implementation 'com.google.code.gson:gson:2.13.1'

    implementation "com.google.accompanist:accompanist-drawablepainter:$accompanist_version"

    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation platform('com.squareup.okhttp3:okhttp-bom:4.12.0')
    implementation 'com.squareup.okhttp3:okhttp'
    implementation 'com.squareup.okhttp3:logging-interceptor'

    implementation "io.coil-kt.coil3:coil-compose:$coil_version"
    implementation "io.coil-kt.coil3:coil-network-okhttp:$coil_version"

    implementation "org.mobilenativefoundation.store:store5:$store_version"

    implementation 'com.jakewharton.timber:timber:5.0.1'

    implementation "io.github.raamcosta.compose-destinations:core:$compose_destinations_version"
    ksp "io.github.raamcosta.compose-destinations:ksp:$compose_destinations_version"

    implementation 'com.github.haroldadmin:NetworkResponseAdapter:5.0.0'

    implementation "com.mikepenz:aboutlibraries-core:$aboutlibs_version"
    implementation "com.mikepenz:aboutlibraries-compose-m3:$aboutlibs_version"

    implementation 'io.github.dokar3:chiptextfield-m3:0.7.0-alpha05'

    implementation "ch.acra:acra-mail:$acra_version"
    implementation "ch.acra:acra-dialog:$acra_version"

    implementation 'com.colintheshots:twain:0.3.2'

    implementation 'sh.calvin.reorderable:reorderable:2.5.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.8.3'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestImplementation 'tools.fastlane:screengrab:2.1.1'

    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
}

kapt {
    correctErrorTypes true
}

aboutLibraries {
	// Remove the "generated" timestamp to allow for reproducible builds
	excludeFields = ['generated']
}

